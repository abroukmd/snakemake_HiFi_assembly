#############################################
# Snakefile — HiFi Assembly
#############################################

import pandas as pd
from pathlib import Path
import os
import sys

# ──────────────────────────────────────────────────
# Validate and load config
# ──────────────────────────────────────────────────

sample_sheet = config.get("sample_sheet", "").strip()
if not sample_sheet:
    raise ValueError("Missing 'sample_sheet' in config file.")

project_dir = config.get("project_dir", "").strip().rstrip("/")
if not project_dir:
    raise ValueError("Missing 'project_dir' in config file.")

project_dir = os.path.abspath(project_dir)
os.makedirs(project_dir, exist_ok=True)

def outpath(*parts):
    """Helper to construct absolute paths under project_dir."""
    return os.path.join(project_dir, *parts)

# ──────────────────────────────────────────────────
# Load sample metadata and validate presence of all listed files
# ──────────────────────────────────────────────────

samples_df = pd.read_csv(sample_sheet, sep="\t")
required_columns = {"sample", "HiFi", "Ref", "fq1", "fq2"}
missing = required_columns - set(samples_df.columns)
if missing:
    raise ValueError(f"Missing columns in sample sheet: {missing}")

samples = samples_df["sample"].tolist()
sample_paths = dict(zip(samples_df["sample"], samples_df["HiFi"]))
sample_refs  = dict(zip(samples_df["sample"], samples_df["Ref"]))
sample_fq1   = dict(zip(samples_df["sample"], samples_df["fq1"]))
sample_fq2   = dict(zip(samples_df["sample"], samples_df["fq2"]))

# Optional: RagTag flag
if "ragtag" in samples_df.columns:
    RAGTAG_SAMPLES = samples_df.query("ragtag == 'Y'")["sample"].tolist()
else:
    RAGTAG_SAMPLES = []

print("RAGTAG_SAMPLES =", RAGTAG_SAMPLES)

globals().update({
    "sample_fq1": sample_fq1,
    "sample_fq2": sample_fq2,
    "RAGTAG_SAMPLES": RAGTAG_SAMPLES
})

# Validate existence of essential files
for s in samples:
    for path in [sample_paths[s], sample_fq1[s], sample_fq2[s]]:
        if not Path(path).is_file():
            print(f"[ERROR] Missing input file for sample {s}: {path}")
            dir_path = Path(path).parent
            if dir_path.exists():
                print(f"[DEBUG] Contents of {dir_path}:")
                for p in sorted(dir_path.glob('*')):
                    print(f"  - {p.name}")
            raise FileNotFoundError(f"Input file not found for sample {s}: {path}")

def reference_from_tsv(sample):
    return sample_refs[sample]

SAMPLES = samples
PLOIDIES = [1, 2, 3, 4]

# ──────────────────────────────────────────────────
# Include modular rule files
# ──────────────────────────────────────────────────

include: "rules/qc.smk"
include: "rules/assembly.smk"
include: "rules/purge_dups.smk"
include: "rules/oatk.smk"
include: "rules/annotation.smk"
include: "rules/postqc.smk"
include: "rules/chromeister.smk"
include: "rules/merqury.smk"
include: "rules/ragtag.smk"
include: "rules/chromeister_ragtag.smk"

# ──────────────────────────────────────────────────
# Final target rule
# ──────────────────────────────────────────────────

rule all:
    input:
        # QC outputs
        expand(outpath("Assemblies/{sample}/QC/nanoplot"), sample=SAMPLES),

        # OATK
        expand(outpath("Assemblies/{sample}/OATK/{sample}.mito.ctg.fasta"), sample=SAMPLES),

        # Assemblies
        expand(outpath("Assemblies/{sample}/HIFIASM/hifiasm.done"), sample=SAMPLES),

        # Telosearch
        expand(outpath("Assemblies/{sample}/TELOSEARCH/{sample}.hifireads.fasta"), sample=SAMPLES),

        # rRNA annotation
        expand(outpath("Assemblies/{sample}/FILTERED/barrnap.done"), sample=SAMPLES),

        # Post QC
        expand(outpath("Assemblies/{sample}/QC/BUSCO-hifiasm/busco_hifiasm.done"), sample=SAMPLES),
        expand(outpath("Assemblies/{sample}/QC/BUSCO-purged/busco_purged.done"), sample=SAMPLES),
        expand(outpath("Assemblies/{sample}/QC/QUAST/quast.done"), sample=SAMPLES),
        expand(outpath("Assemblies/{sample}/QC/merqury/merqury.done"), sample=SAMPLES),

        # Chromeister (all samples)
        expand(outpath("Assemblies/{sample}/CHROMEISTER/.hifiasm.done"), sample=SAMPLES),

        # RagTag: ONLY samples with ragtag == Y
        expand(outpath("Assemblies/{sample}/RAGTAG/purged-hifiasm_ragtag/ragtag.scaffold.fasta"), sample=RAGTAG_SAMPLES),
        expand(outpath("Assemblies/{sample}/RAGTAG/purged-hifiasm_ragtag/ragtag.scaffold.fasta.fai"), sample=RAGTAG_SAMPLES),

        # Chromeister on RagTag: ONLY samples with ragtag == Y
        expand(outpath("Assemblies/{sample}/CHROMEISTER_RAGTAG/{sample}-purged_ragtag.mat.png"), sample=RAGTAG_SAMPLES),
        expand(outpath("Assemblies/{sample}/CHROMEISTER_RAGTAG/.purged_ragtag.done"), sample=RAGTAG_SAMPLES)
